task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

allprojects {

    version = '1.3.0-jdk6'
    group = 'com.github.fedxyz'

    repositories {
        mavenCentral()
    }

    apply plugin: 'idea'

    configure(subprojects.findAll { new File(it.projectDir, 'src/main/java').directory }) {
        apply plugin: 'java'
        apply plugin: 'maven-publish'
    }

}

// standard setup for all Java project
subprojects {

    // deploy all Java projects into Bintray JCenter
    if (project.plugins.hasPlugin('java')) {

        // merge Java and YAML files together in one source folder
        sourceSets {
            main {
                resources {
                    srcDir 'src/main/java'
                }
            }
            test {
                resources {
                    srcDir 'src/test/java'
                }
            }
        }

        dependencies {
            // test support
            testCompile "org.slf4j:slf4j-simple:${slf4jVersion}"
            testCompile "junit:junit:4.12"
        }

        task sourceJar(type: Jar) {
            from sourceSets.main.allSource
            classifier = 'sources'
        }
        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        publishing {
            publications {
                maven(MavenPublication) {
                    groupId project.group
                    artifactId project.name
                    version project.version
                    from components.java
                }
            }
        }
    }
}
